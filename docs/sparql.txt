--
# Goal: find enriched calcium_ion_homeostasis biological processes (bp) having participant of proteins 
#       that are the targets of chemicals/drugs that inhibit coronaviral infection in vivo or in vitro.
#       Then list related information.  

PREFIX chemical_entity: <http://purl.obolibrary.org/obo/CHEBI_24431>
PREFIX protein: <http://purl.obolibrary.org/obo/PR_000000001>
PREFIX chemical_has_protein_target_as_inhibitor: <http://purl.obolibrary.org/obo/CIDO_0000119>
PREFIX calcium_ion_homeostasis: <http://purl.obolibrary.org/obo/GO_0055074>
PREFIX participates_in: <http://purl.obolibrary.org/obo/RO_0000056>

SELECT distinct ?bp STR(?bp_label) ?protein_target STR(?pt_label) ?chemical STR(?chemical_label)
FROM <http://purl.obolibrary.org/obo/merged/CIDO>
WHERE
{
   ?chemical rdfs:subClassOf chemical_entity: option(transitive) .

   ?chemical rdfs:subClassOf ?restriction .
   ?restriction owl:onProperty ?chemical_has_protein_target_as_inhibitor; owl:someValuesFrom ?protein_target .
   ?protein_target rdfs:subClassOf protein: option(transitive) .
   ?protein_target rdfs:subClassOf ?protein_target_restriction .
   ?protein_target_restriction owl:onProperty participates_in:; owl:someValuesFrom ?bp .
   ?bp rdfs:subClassOf calcium_ion_homeostasis: option(transitive) .
   ?bp rdfs:label ?bp_label .
   ?protein_target rdfs:label ?pt_label .
   ?chemical rdfs:label ?chemical_label .
}
       


--
# Date: 10/13/2020 -- by Oiver He
#Goal: find how many biological processes (bp) having participant of proteins that are the targets of 
#     chemicals/drugs that inhibit coronaviral infection in vivo or in vitro. 

PREFIX protein: <http://purl.obolibrary.org/obo/PR_000000001>
PREFIX chemical_has_protein_target_as_inhibitor: <http://purl.obolibrary.org/obo/CIDO_0000119>
PREFIX biological_process_root: <http://purl.obolibrary.org/obo/GO_0008150>
PREFIX participates_in: <http://purl.obolibrary.org/obo/RO_0000056>

SELECT count(distinct ?bp)
FROM <http://purl.obolibrary.org/obo/merged/CIDO>
WHERE
{
   ?chemical rdfs:subClassOf ?restriction .
   ?restriction owl:onProperty ?chemical_has_protein_target_as_inhibitor; owl:someValuesFrom ?protein_target .
   ?protein_target rdfs:subClassOf protein: option(transitive) .
   ?protein_target rdfs:subClassOf ?protein_target_restriction .
   ?protein_target_restriction owl:onProperty participates_in:; owl:someValuesFrom ?bp .
   ?bp rdfs:subClassOf biological_process_root: option(transitive) .
}
       
--
# Date: 10/12/2020 -- by Oiver He
# Goal: find GO processes that have participant some proteins which are the targets of some drugs.
 
PREFIX chemical_entity: <http://purl.obolibrary.org/obo/CHEBI_24431>
PREFIX protein: <http://purl.obolibrary.org/obo/PR_000000001>
PREFIX chemical_has_protein_target: <http://purl.obolibrary.org/obo/CIDO_0000118>
PREFIX calcium_ion_homeostasis: <http://purl.obolibrary.org/obo/GO_0055074>
PREFIX participates_in: <http://purl.obolibrary.org/obo/RO_0000056>

SELECT distinct STR(?bp_label) ?bp STR(?chemical_label) STR(?protein_target_label)
FROM <http://purl.obolibrary.org/obo/merged/CIDO>
WHERE
{
   ?chemical rdfs:subClassOf chemical_entity: option(transitive) .
   ?chemical rdfs:label ?chemical_label .

   ?chemical rdfs:subClassOf ?chemical_restriction2 .
   ?chemical_restriction2 owl:onProperty ?chem_has_protein_target; owl:someValuesFrom ?protein_target .
   ?chem_has_protein_target rdfs:subPropertyOf chemical_has_protein_target: option(transitive) . 
   ?protein_target rdfs:subClassOf protein: option(transitive) .

   ?protein_target rdfs:subClassOf ?protein_target_restriction .
   ?protein_target_restriction owl:onProperty participates_in:; owl:someValuesFrom ?bp .
   ?bp rdfs:subClassOf calcium_ion_homeostasis: option(transitive) .

   ?bp rdfs:label ?bp_label .
   ?protein_target rdfs:label ?protein_target_label .
}
       

--
# Date: 9/26/2020 -- by Oiver He
# note: result: 28 
# Goal: to query CIDO to find the number of chemicals capable of inhibiting the invasion of conoravirues

PREFIX Coronaviridae: <http://purl.obolibrary.org/obo/NCBITaxon_11118>
PREFIX chemical_inhibits_invasion_of_virus: <http://purl.obolibrary.org/obo/CIDO_0000149>
SELECT count(distinct ?chemical) 
FROM <http://purl.obolibrary.org/obo/merged/CIDO>
WHERE
{
   ?chemical rdfs:label ?chemical_label .

   ?chemical rdfs:subClassOf ?chemical_restriction .
   ?chemical_restriction owl:onProperty ?chemical_inhibits_invasion_of_virus; owl:someValuesFrom ?virus .
   ?chemical_inhibits_invasion_of_virus rdfs:subPropertyOf chemical_inhibits_invasion_of_virus: option(transitive) . 
   ?virus rdfs:subClassOf Coronaviridae: option(transitive) .
}

--
# Date: 9/26/2020 -- by Oiver He
# Goal: to query CIDO to find chemicals capable of inhibiting the invasion of conoravirues, 
# and are able to inhibit protein(s) participating in the biological process of calcium ion hoemeostasis. 
#note: only two results shown. need to double check the outcome. 

PREFIX chemical_entity: <http://purl.obolibrary.org/obo/CHEBI_24431>
PREFIX protein: <http://purl.obolibrary.org/obo/PR_000000001>
PREFIX Coronaviridae: <http://purl.obolibrary.org/obo/NCBITaxon_11118>
PREFIX chemical_inhibits_invasion_of_virus: <http://purl.obolibrary.org/obo/CIDO_0000149>
PREFIX chemical_has_protein_target: <http://purl.obolibrary.org/obo/CIDO_0000118>
PREFIX calcium_ion_homeostasis: <http://purl.obolibrary.org/obo/GO_0055074>
PREFIX participates_in: <http://purl.obolibrary.org/obo/RO_0000056>

SELECT distinct ?chemical_label ?chemical 
FROM <http://purl.obolibrary.org/obo/merged/CIDO>
WHERE
{
   ?chemical rdfs:subClassOf chemical_entity: option(transitive) .
   ?chemical rdfs:label ?chemical_label .

   ?chemical rdfs:subClassOf ?chemical_restriction .
   ?chemical_restriction owl:onProperty ?chemical_inhibits_invasion_of_virus; owl:someValuesFrom ?virus .
   ?chemical_inhibits_invasion_of_virus rdfs:subPropertyOf chemical_inhibits_invasion_of_virus: option(transitive) . 
   ?virus rdfs:subClassOf Coronaviridae: option(transitive) .

   ?chemical rdfs:subClassOf ?chemical_restriction2 .
   ?chemical_restriction2 owl:onProperty chemical_has_protein_target:; owl:someValuesFrom ?protein_target .
   ?protein_target rdfs:subClassOf protein: option(transitive) .

   ?protein_target rdfs:subClassOf ?protein_target_restriction .
   ?protein_target_restriction owl:onProperty participates_in:; owl:someValuesFrom ?cal_ion_homeostasis .
   ?cal_ion_homeostasis rdfs:subClassOf calcium_ion_homeostasis: option(transitive) .
}

--
#note: this script is similar to the above but with two lines of difference; however, it took a lot time to run. The result is slightly different from the DL query.   

# Goal: to query CIDO to find chemicals capable of inhibiting the invasion of conoravirues, 
# and are able to inhibit protein(s) participating in the biological process of calcium ion hoemeostasis. 

PREFIX chemical_entity: <http://purl.obolibrary.org/obo/CHEBI_24431>
PREFIX protein: <http://purl.obolibrary.org/obo/PR_000000001>
PREFIX Coronaviridae: <http://purl.obolibrary.org/obo/NCBITaxon_11118>
PREFIX chemical_inhibits_invasion_of_virus: <http://purl.obolibrary.org/obo/CIDO_0000149>
PREFIX chemical_has_protein_target: <http://purl.obolibrary.org/obo/CIDO_0000118>
PREFIX calcium_ion_homeostasis: <http://purl.obolibrary.org/obo/GO_0055074>
PREFIX participates_in: <http://purl.obolibrary.org/obo/RO_0000056>

SELECT distinct ?chemical_label ?chemical 
FROM <http://purl.obolibrary.org/obo/merged/CIDO>
WHERE
{
   ?chemical rdfs:subClassOf chemical_entity: option(transitive) .
   ?chemical rdfs:label ?chemical_label .

   ?chemical rdfs:subClassOf ?chemical_restriction .
   ?chemical_restriction owl:onProperty ?chemical_inhibits_invasion_of_virus; owl:someValuesFrom ?virus .
   ?chemical_inhibits_invasion_of_virus rdfs:subPropertyOf chemical_inhibits_invasion_of_virus: option(transitive) . 
   ?virus rdfs:subClassOf Coronaviridae: option(transitive) .

   ?chemical rdfs:subClassOf ?chemical_restriction2 .
   ?chemical_restriction2 owl:onProperty ?chem_has_protein_target; owl:someValuesFrom ?protein_target .
   ?chem_has_protein_target rdfs:subPropertyOf chemical_has_protein_target: option(transitive) . 
   ?protein_target rdfs:subClassOf protein: option(transitive) .

   ?protein_target rdfs:subClassOf ?protein_target_restriction .
   ?protein_target_restriction owl:onProperty participates_in:; owl:someValuesFrom ?cal_ion_homeostasis .
   ?cal_ion_homeostasis rdfs:subClassOf calcium_ion_homeostasis: option(transitive) .
}
--
# Date: 9/26/2020 -- by Oiver He
# Goal: to query CIDO to find typical digestive phenotypes associated with COVID-19

PREFIX disease_susceptibly_has_phenotype: <http://purl.obolibrary.org/obo/CIDO_0000173>
PREFIX COVID_19_disease_process: <http://purl.obolibrary.org/obo/CIDO_0000003>
PREFIX digestive_abnormality: <http://purl.obolibrary.org/obo/HP_0025031>
SELECT distinct ?disease_label STR(?phenotype_label) as ?pheontype_label ?phenotype
FROM <http://purl.obolibrary.org/obo/merged/CIDO>
WHERE
{
   COVID_19_disease_process: rdfs:label ?disease_label .
   ?phenotype rdfs:label ?phenotype_label . 
   COVID_19_disease_process: rdfs:subClassOf ?phenotype_restriction .
   ?phenotype_restriction owl:onProperty disease_susceptibly_has_phenotype:; owl:someValuesFrom ?phenotype .
   ?phenotype rdfs:subClassOf digestive_abnormality: option(transitive) .
}


--
# Goal: To find all drug chemicals in CIDO that have different roles
# Note: the following code appears to have some bug. 

PREFIX chemical_entity: <http://purl.obolibrary.org/obo/CHEBI_24431>
PREFIX has_role: <http://purl.obolibrary.org/obo/RO_0000087>
PREFIX antiviral_agent: <http://purl.obolibrary.org/obo/CHEBI_22587>
PREFIX antimalarial: <http://purl.obolibrary.org/obo/CHEBI_38068>
PREFIX anti_inflammatory_agent: <http://purl.obolibrary.org/obo/CHEBI_67079>
SELECT count(DISTINCT ?chemical)
#SELECT DISTINCT ?chemical ?label
from <http://purl.obolibrary.org/obo/merged/CIDO>
WHERE
{
?chemical rdfs:subClassOf chemical_entity: option (transitive) . 
?chemical rdfs:label ?label .

?chemical rdfs:subClassOf ?restriction1 . 
   ?restriction1 owl:onProperty has_role:; owl:someValuesFrom ?antiviral_agent . 

?chemical rdfs:subClassOf ?restriction2 . 
   ?restriction2 owl:onProperty has_role:; owl:someValuesFrom ?antimalarial . 

?chemical rdfs:subClassOf ?restriction3 . 
   ?restriction3 owl:onProperty has_role:; owl:someValuesFrom ?anti_inflammatory_agent . 
}

--
# Goal: This SPARQL query is to query NDF-RT. 

PREFIX may_treat: <http://evs.nci.nih.gov/ftp1/NDF-RT/NDF-RT.owl#may_treat>
PREFIX InfectDisTop: <http://evs.nci.nih.gov/ftp1/NDF-RT/NDF-RT.owl#N0000000007>
PREFIX EnzymeInhibitorMoA: <http://evs.nci.nih.gov/ftp1/NDF-RT/NDF-RT.owl#N0000000133>
PREFIX has_MoA: <http://evs.nci.nih.gov/ftp1/NDF-RT/NDF-RT.owl#has_MoA>
SELECT distinct  ?drug ?drug_label
from <http://purl.obolibrary.org/obo/merged/CIDO>
# from <http://evs.nci.nih.gov/ftp1/NDF-RT/NDF-RT.owl>

WHERE
{  
   ?drug rdfs:label ?drug_label .
   ?drug rdfs:subClassOf <http://evs.nci.nih.gov/ftp1/NDF-RT/NDF-RT.owl#N0000000001> option (transitive) .
     
   ?id rdfs:label ?id_label .
   ?id rdfs:subClassOf InfectDisTop: option (transitive) .

   ?MoA rdfs:label ?MoA_label .
   ?MoA rdfs:subClassOf EnzymeInhibitorMoA: option (transitive) .

   ?drug rdfs:subClassOf ?drug_restriction1 .
   ?drug_restriction1 owl:onProperty may_treat:; owl:someValuesFrom ?id .

   ?drug rdfs:subClassOf ?drug_restriction2 .
   ?drug_restriction2 owl:onProperty has_MoA:; owl:someValuesFrom ?MoA .

}



--
# Note: This SPARQL query is to query the whole NDF-RT ontology stored in Ontobee. Oliver - May 23, 2020. 
# N0000000001 and N0000000007 are the IDs of the top level drug and infectious disease terms, respectively, in NDF-RT. 
# Goal: Identify drugs that have specific MoAs as enzyme inhibitors. 

PREFIX may_treat: <http://evs.nci.nih.gov/ftp1/NDF-RT/NDF-RT.owl#may_treat>
PREFIX InfectDisTop: <http://evs.nci.nih.gov/ftp1/NDF-RT/NDF-RT.owl#N0000000007>
PREFIX EnzymeInhibitorMoA: <http://evs.nci.nih.gov/ftp1/NDF-RT/NDF-RT.owl#N0000000133>
PREFIX has_MoA: <http://evs.nci.nih.gov/ftp1/NDF-RT/NDF-RT.owl#has_MoA>
SELECT distinct  ?drug ?drug_label
from <http://evs.nci.nih.gov/ftp1/NDF-RT/NDF-RT.owl>
WHERE
{  
   ?drug rdfs:label ?drug_label .
   ?drug rdfs:subClassOf <http://evs.nci.nih.gov/ftp1/NDF-RT/NDF-RT.owl#N0000000001> option (transitive) .
     
   ?id rdfs:label ?id_label .
   ?id rdfs:subClassOf InfectDisTop: option (transitive) .

   ?MoA rdfs:label ?MoA_label .
   ?MoA rdfs:subClassOf EnzymeInhibitorMoA: option (transitive) .

   ?drug rdfs:subClassOf ?drug_restriction1 .
   ?drug_restriction1 owl:onProperty may_treat:; owl:someValuesFrom ?id .

   ?drug rdfs:subClassOf ?drug_restriction2 .
   ?drug_restriction2 owl:onProperty has_MoA:; owl:someValuesFrom ?MoA .

}